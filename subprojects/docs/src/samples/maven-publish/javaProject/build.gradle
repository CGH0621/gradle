apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'org.gradle.sample'
version = '1.0'

dependencies {
   compile 'commons-collections:commons-collections:3.2.2'
}

repositories {
    mavenCentral()
}

// START SNIPPET publish-custom-artifact
task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}
// END SNIPPET publish-custom-artifact

// START SNIPPET repo-url-from-variable
ext {
    // change to point to your repositories, e.g. http://my.org/repos/releases
    releasesRepoUrl = "$buildDir/repos/releases"
    snapshotsRepoUrl = "$buildDir/repos/snapshots"
}

publishing {
    repositories {
        maven {
            url version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}
// END SNIPPET repo-url-from-variable
